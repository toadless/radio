plugins{
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'nu.studer.jooq' version '5.2.1'
}
mainClassName = 'net.toadless.radio.Main'
sourceCompatibility = 14

repositories {
    mavenCentral()
    maven { url 'https://m2.dv8tion.net/releases' }
    maven { url 'https://jitpack.io' }
}

dependencies {

    //Misc / Util
    implementation 'info.debatty:java-string-similarity:2.0.0'
    implementation 'io.github.classgraph:classgraph:4.8.138'
    implementation 'ch.qos.logback:logback-classic:1.2.10'
    implementation 'net.jodah:expiringmap:0.5.10'
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:7.0.0'

    //Web
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'io.javalin:javalin:4.3.0'

    //Discord
    implementation 'net.dv8tion:JDA:4.4.0_352'
    implementation 'com.sedmelluq:lavaplayer:1.3.78'

    //Database
    implementation 'org.postgresql:postgresql:42.3.1'
    jooqGenerator 'org.postgresql:postgresql:42.3.1'

    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.jooq:jooq:3.16.4'
}

jooq {
    version = '3.14.4'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    url = RADIO_DB_URL
                    user = RADIO_DB_USER
                    password = RADIO_DB_PASSWORD
                    driver = 'org.postgresql.Driver'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'net.toadless.radio.jooq'
                        directory = "src/main/jooq"
                        clean = true
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}


compileJava.options.encoding = 'UTF-8'